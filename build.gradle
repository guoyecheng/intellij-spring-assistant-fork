import com.vladsch.flexmark.html.HtmlRenderer
import com.vladsch.flexmark.parser.Parser
import com.vladsch.flexmark.util.ast.Document

buildscript {
    repositories {
        mavenLocal()
        maven {
            url 'https://maven.aliyun.com/repository/public'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.vladsch.flexmark:flexmark:0.62.2'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.intellij' version '1.15.0'
}


group = 'in.oneton.idea.spring'
version = '2023.2'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

repositories {
    mavenLocal()
    maven {
        url 'https://maven.aliyun.com/repository/public'
    }
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    testCompileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {

    ['annotationProcessor', 'compileOnly', 'testAnnotationProcessor', 'testCompileOnly']
            .each { conf -> add(conf, 'org.projectlombok:lombok:1.18.20') }

    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'com.miguelfonseca.completely:completely-core:0.9.0'

    testImplementation(platform('org.junit:junit-bom:5.7.2'))
    testImplementation 'org.mockito:mockito-core:5.4.0'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
}
clean.doFirst {
    delete 'build', 'out'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    type = 'IC'
//    version = '232.8453-EAP-CANDIDATE-SNAPSHOT'
    version = '2023.2'
    pluginName = 'intellij-spring-helper'
    plugins = ['java', 'properties', 'yaml', 'maven', 'gradle']
    updateSinceUntilBuild = false
}

tasks{
//    publishPlugin {
//        token System.getenv('IJ_PLUGIN_TOKEN')
//        //    username System.getProperty('ij_plugin_portal_login')
//        //    password System.getProperty('ij_plugin_portal_password')
//        channels 'eap', 'nightly', 'default'
//    }
    patchPluginXml {
        sinceBuild = '232'
        untilBuild = '232.*'
        pluginDescription = provider {readmeXmlAsHtml()}
        changeNotes = provider {changeLogAsHtml()}
    }
}

String readmeXmlAsHtml() {
    final String readmeContent = new File(rootProject.uri('README.md')).text
    // since these images needs to shown from within intellij, lest put absolute urls so that the images & changelog will be visible
            .replaceAll("help\\.gif", "https://raw.githubusercontent.com/cheng6563/intellij-spring-assistant/" + "1.0.6" + "/help.gif")
            .replaceAll("CHANGELOG.md", "https://github.com/cheng6563/intellij-spring-assistant/blob/" + "1.0.6" + "/CHANGELOG.md")

    final Document readmeDocument = Parser.builder().build().parse(readmeContent)
    HtmlRenderer.builder().build().render(readmeDocument)
}

String changeLogAsHtml() {
    Parser parser = Parser.builder().build()
    HtmlRenderer renderer = HtmlRenderer.builder().build()
    Document changeLogDocument = parser.parse(new File(rootProject.uri('CHANGELOG.md')).text)
    renderer.render(changeLogDocument)
}
